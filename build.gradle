plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
}

version = wilderForgeVersion
group = "com.wildermods"
archivesBaseName = "wilderforge"

apply plugin: 'com.wildermods.workspace'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://maven.fabricmc.net/"
    }
    maven {
        url "https://maven.wildermods.com/"
    }
}

sourceCompatibility = '17'
targetCompatibility = '17'

dependencies {

	implementation libs.reflections8
	implementation libs.commons.lang
	
	/**********
	don't alter the dependencies below
	***********/
	
	//Wildermyth, Scratchpad
	compileOnly fileTree(dir: './bin/', include: ['*.jar'])
	
	//All dependencies of Wildermyth itself
	compileOnly fileTree(dir: './bin/lib/', include: ['*.jar'])
}

import org.apache.tools.ant.filters.ReplaceTokens
def dontFilterExtensions = ['**/*.png', '**/*.jpg']

// Task to copy and filter files (excluding binary files)
task filterResources(type: Sync) {
    from(sourceSets.main.resources.srcDirs) {
        dontFilterExtensions.each { exclude it } // Exclude binary files
        filter(ReplaceTokens, tokens: [
            wilderForgeVersion: wilderForgeVersion
        ])
    }
    into "$buildDir/processedSrcFiltered"
}

// Task to copy binary files without filtering
task copyUnfilteredResources(type: Sync) {
    from(sourceSets.main.resources.srcDirs) {
        dontFilterExtensions.each { include it } // Include only binary files
    }
    into "$buildDir/processedSrcUnfiltered"
}

// Combine the filtered and unfiltered resources
task processSource(type: Sync) {
    dependsOn filterResources, copyUnfilteredResources

	from(sourceSets.main.java.srcDirs) // Include Java source files
	from(sourceSets.main.resources.srcDirs)
    from("$buildDir/processedSrcFiltered")
    from("$buildDir/processedSrcUnfiltered")

	duplicatesStrategy = DuplicatesStrategy.INCLUDE

    into "$buildDir/processedSrc"
}

processResources {
    from "$buildDir/processedSrc"
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    dependsOn processSource
}

compileJava {
    source = fileTree(dir: "$buildDir/processedSrc", include: ['**/*'])
    dependsOn processSource
}

ext.mavenLocalUrl = repositories.mavenLocal().url.toString()

jar {
	manifest {
		attributes(
			'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
			'MixinConfigs': 'mixins.json',
			'Main-Class': 'net.fabricmc.loader.impl.launch.knot.KnotClient',
			'Specification-Version': 8.0,
			'Multi-Release': 'true'
		)
	}
}

task sourceJar(type: Jar) {
    from "$buildDir/processedSrc" // Use the processed source directory
    archiveClassifier.set('sources')
    
    // Declare processSource as an input of sourceJar to ensure proper task ordering
    inputs.files tasks.processSource.outputs.files
    
    dependsOn processSource
}
build.dependsOn(sourceJar)

tasks.register('checkArtifactExists') {
    doLast {
    
    	if (project.hasProperty('force')) {
			logger.lifecycle("Skipping artifact existence check due to --force flag.")
			return
		}
		
        def repoUrl = project.hasProperty('mavenRepoUrl') ? project.mavenRepoUrl : mavenLocalUrl
        def artifactPath = "${repoUrl}/${project.group.replace('.', '/')}/${project.archivesBaseName}/${project.version}/${project.archivesBaseName}-${project.version}.jar"
        logger.lifecycle("Checking if artifact exists at: $artifactPath")
        
        if (artifactPath.startsWith('file:/')) {
            // Handle file URLs
            def file = new File(new URI(artifactPath))
            if (file.exists()) {
                throw new IllegalStateException("Artifact '${project.group}:${project.archivesBaseName}:${project.version}' already exists. Publishing aborted.")
            }
        } else {
            // Handle HTTP URLs
            def url = new URL(artifactPath)
            def connection = url.openConnection()
            connection.setRequestMethod('HEAD')
            if (connection.responseCode == 200) {
				throw new IllegalStateException("Artifact '${project.group}:${project.archivesBaseName}:${project.version}' already exists. Publishing aborted.")
            }
        }
        logger.lifecycle("Artifact does not exist, proceeding with publish.")
    }
}

tasks.named('publish') {
    dependsOn 'checkArtifactExists'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version

            // Attach sources JAR to the publication
            artifact sourceJar
        }
    }

    repositories {
        maven {
            url = uri(project.hasProperty('mavenRepoUrl') ? project.mavenRepoUrl : mavenLocalUrl) // Default to mavenLocal if no custom URL is provided
        }
    }
}
