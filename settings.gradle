pluginManagement {
	def getVersionFromToml = { versionKey ->
	    def tomlFile = file("gradle/libs.versions.toml")
	    if (!tomlFile.exists()) {
	        throw new GradleException("libs.versions.toml file not found!")
	    }
	
	    def versionPattern = ~/^\s*${versionKey}\s*=\s*"(.*?)"\s*$/
	    
	    def version = null
	    
	    tomlFile.eachLine { line ->
	        //println "Checking line: ${line}" 
	        def matcher = line =~ versionPattern
	        if (matcher.find()) {
	            println "Found ${versionKey}: ${matcher.group(1)}"
	            version = matcher.group(1)
	        }
	    }
	    
	    if(version != null) {
	    	return version;
	    }

	    println "Version '${versionKey}' not found in gradle/libs.versions.toml"
	    throw new GradleException("Version '${versionKey}' not found in libs.versions.toml")
	}
    repositories {
    	mavenLocal();
    	mavenCentral();
    	maven {
    		url "https://maven.fabricmc.net/"
    	}
        maven {
            url 'https://maven.wildermods.com'
        }
        maven {
        	url 'https://central.sonatype.com/repository/maven-snapshots/'
        }
    }
    plugins {
        id 'com.wildermods.workspace' version getVersionFromToml('workspace_version')
    }
}
plugins {
    id 'com.wildermods.workspace'
}
rootProject.name = "wilderforge"